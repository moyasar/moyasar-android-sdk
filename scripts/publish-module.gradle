apply plugin: 'maven-publish'
apply plugin: 'signing'

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                // Mostly self-explanatory metadata
                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Moyasar official Android SDK'
                    url = 'https://moyasar.com'
                    licenses {
                        license {
                            name = 'MIT License'
                            comments = new File("${rootDir}/LICENSE.md").text
                        }
                    }
                    developers {
                        developer {
                            id = 'developers'
                            name = 'Moyasar Developers'
                            email = 'developers@moyasar.com'
                        }
                        developer {
                            id = 'ahoshaiyan'
                            name = 'Ali Alhoshaiyan'
                            email = 'a.hoshaiyan@moyasar.com'
                        }
                    }
                    scm {
                        url = 'https://moyasar.com'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
        rootProject.ext["signing.keyId"],
        rootProject.ext["signing.key"],
        rootProject.ext["signing.password"],
    )
    sign publishing.publications
}
